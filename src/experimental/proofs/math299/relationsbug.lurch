include libs/Relations
////////////////////////////////////////////////////////////////////
// Relation Test Proofs
// 

➤ "Math 299 Relations Homework Proofs 2023"

{ // wrap them all

{
	➤ "Assignment 22 - Problem 4"
	
	{
		➤ "Theorem 10.65: Congruence mod m is an equivalence relation."
		
		// Theorem: { '~' is an equivalence relation }
	  ➤ "Proof:"
	  { 
			➤ "Define '~' to be congruence mod m."
			Let m
			Definition: :{ x~y ≡ x ≈ y mod m } 
			➤ "Reflexive:"
			{ Let x
        
        x-x = 0
				    = m⋅0
			  
				∃d. x-x = m⋅d		
			  m | x-x
				x ≈ x mod m
				x ~ x
			}
	  	'~' is reflexive
			
			➤ "Symmetric:"
			{ Let x,y such that x~y
        
				x ≈ y mod m
			  m | x-y
        x-y = m⋅k for some k
        
				y-x = 0+(y-x)
				    = (-(x-y) + (x-y))+(y-x)
				    = -(x-y) + ((x-y)+(y-x))
				    = -(x-y) + (x+(-y+(y-x)))
				    = -(x-y) + (x+((-y+y)-x))
				    = -(x-y) + (x+(0-x))
				    = -(x-y) + (x-x)
				    = -(x-y) + 0
				    = -(x-y)
				    = -(m⋅k)
				    = -(m⋅k)  + 0
				    = -(m⋅k)  + m⋅0
				    = -(m⋅k)  + m⋅(k-k)
				    = -(m⋅k)  + (m⋅k + m⋅(-k))
				    = (-(m⋅k) + m⋅k) + m⋅(-k)
						= 0 + m⋅(-k)
						= m⋅(-k)
				
				∃d.y-x=m⋅d
				m | y-x
				y ≈ x mod m
				y ~ x
			}
			'~' is symmetric
			{ Let x,y,z such that x~y and y~z
				x~y 
				y~z
        x ≈ y mod m
        y ≈ z mod m
				m | x-y
				m | y-z
				// TODO: why does changing i to k on the next line and below cause a problem?
				x-y = m⋅k for some k
				y-z = m⋅j for some j

				x-z = (x+0)-z
				    = (x+(-y+y))-z
				    = ((x-y)+y)-z
				    = (x-y)+(y-z)
						= m⋅k+m⋅j
						= m⋅(k+j)

				∃d.x-z=m⋅d
				m | x-z
				x ≈ z mod m
				x~z
			}
			'~' is transitive
	  }
  }
}

// end of all
} 
