///////////////////////////////////////////////////////////////////////////
// AsciiMath Peggy Grammar and Parser
//
// A peggy grammar definition file to generate a parser for converting
// AsciiMath expression to an LC.
//
// Note: for now we encode negative numbers as compound expressions e.g. -3 is
// encoded as (- 3). We also encode rational fractions as a product of the
// numerator times the multiplicative inverse of the denominator where / is the
// unary inverse operator, e.g. 2/3 parses as (â‹… 2 (/ 3)).  This is consistent
// with the way negation is handled.  We do not allow expresions like '/2' to
// represent one half. We do not have an Integer or Rational constant type for
// this reason.
//
// Note: Declare's have to be on a line by themselves and its declared
// constants separated by spaces (not commas)
//
// To save the resulting parser to a standalone .js file use:
//   peggy --cache --format es -o asciimath.js asciimath.peggy
//
//
// TODO: 
//  * add equiv classes, infix relation symbols family

{{

  // remove empty arrays from a nested array
  const clean = A => { 
    return A.map( c => { 
      return (Array.isArray(c) && c.length) ? clean(c) : c 
      } ).filter( e => !(Array.isArray(e) && e.length===0) )
  }
  
  // replace the commas in a sequence with spaces
  const spaced = s => s.replace(/,/g, ' ') 

  // tex utilities
  const texUnary = (op,arg) => {
    return (op) ? `${op} ${arg}` : arg
  }

  const texJoin = (op,args) => {
    if (args.length===1) return args.join(op)
    return args.map(x=>` ${x}`).join(op)
  }

  const texRightAssoc = (op,args) => {
    return args.reverse().slice(1).reduce(
      (ans,x)=>{ return `${x}${op}{${ans}}`},args[0])
  }

// convert signed sums to lisp
  const texSum = (first,rest) => {
    let ans = `${first}`
    rest.forEach( term => {
      ans = ans + ( (term[0]==='-') ? `-${term[1]}` : `+${term[1]}` )
    })
    return ans
  }

  // default: convert optional associative binary operator to lisp
  const lisp = (op,args) => {
    // if there's only one arg, return it, otherwise apply the op
    return (args.length>1) ? `(${op} ${args.join(' ')})` : args[0]
  }

  // convert optional unary operator to lisp
  const lispUnary = (op,arg) => {
    return (op) ? `(${op} ${arg})` : arg
  }

  // convert mandatory binary operator to lisp
  const lispBinary = (op,a,b) => {
    return `(${op} ${a} ${b})`
  }

  // convert prefix function application to lisp
  const lispPrefix = (op,args) => {
    if (!Array.isArray(args)) { return `(${op} ${args})` }
    else if (!args.every(Array.isArray)) { 
      return `(${op} ${args.join(' ')})` 
    } else {
      return args.reduce( (ans,group) => { 
        return (group.length) ? `(${ans} ${group.join(' ')})` : `(${ans})`  
      } , op )
    }
  }

  // convert signed sums to lisp
  const lispSum = (first,rest) => {
    let ans = `(+ ${first}`
    rest.forEach( term => {
      ans = ans + ( (term[0]==='-') ? ` (- ${term[1]})` : ` ${term[1]}` )
    })
    return ans + ')'
  }

  // for debugging, shorthand for console.log
  const say = s => {
    const lines = s.split('\n')
    const lineNumberWidth = String(lines.length).length
    lines.forEach( (line, index) => {
      const lineNumber = String(index + 1).padStart(lineNumberWidth, ' ')
      console.log(`${lineNumber}: ${line}`)
    })
  }

}}

// Preprocess the input string
{ 
  //   * replace toxic unicode chars with equivalents
  input = input.replace(/ðœŽ/g, 'Ïƒ')
  input = input.replace(/ðœ†/g, 'Î»')
  input = input.replace(/â‰ /g, ' neq ')  
  input = input.replace(/âˆ‰/g, ' notin ') 
  
  //   * since we want to use â†’ for maps, we replace the contradiction symbol pair up front
  input = input.replace(/â†’â†/g, ' contradiction ')
  //   * we also prefer to write 'equivalence relation', etc. than equivalence_relation
  input = input.replace(/equivalence relation/g, ' equivalence_relation ')
  input = input.replace(/strict partial order/g, ' strict_partial_order ')
  input = input.replace(/partial order/g, ' partial_order ')
  input = input.replace(/total order/g, ' total_order ')
  //   * in order to use ~ and â‰ˆ as both infix operations AND sets (and talk about their
  //     properties) we replace '~' and 'â‰ˆ' up front with (~) and (â‰ˆ) respectively.
  input = input.replace(/'~'/g, '(~)')
  input = input.replace(/'â‰ˆ'/g, '(â‰ˆ)')
  //   * Let's used to require a colon ':Let' but we no longer require it, so 
  //     for backwards compatibility, remove it if its there.
  input = input.replace(/:([Ll]et )/g, '$1') 
  //   * Declaring reserved constants is tricky since it tries to parse them as they are
  //     intended to be used, so for now we convert any line of the form `Declare stuff`
  //     to escaped putdown up front.
  input = input.replace(/[Dd]eclare \s*([^\s].*)$/mg, 'declare> Â«:[$1]Â»\n')
  //   * also look for lines containing only a âž¤ and whitespace, and replace
  //     them with (âž¤ " ") to act as a line break in the output
  input = input.replace(/^([ \t]*)âž¤[ \t]*$/mg, '$1âž¤ " " \n')
  //   * delete comments (but leave any \n's to keep the line counts right)
  input = input.replace(/\/\/[^\n\r]*(\n|\r|$)/g, '\n')
  //   * Replace tabs with a space
  input = input.replace(/\t/g, ' ')
  //   * shrink consecutive spaces to a single space
  input = input.replace(/ ( +)/g,' ') 
  //   * remove spaces around reserved infix unicode ops not inside literal putdown so
  //     the parser doesn't have to find and avoid them
  input = input.replace(/(?<!Â«[^Â«Â»]*)\s*([,.â†¦+â‹…/^=<â‰¤â†’â‡’â‡”|âˆ©âˆªÃ—âˆˆâŠ†âˆ–~â‰ˆ])\s*(?![^Â«Â»]*Â»)/g,'$1')
  //   * same for prefix ops
  input = input.replace(/(?<!Â«[^Â«Â»]*)([-({âˆ€âˆƒ])[ \t]*(?![^Â«Â»]*Â»)/g,'$1')
  //   * same for postfix ops
  input = input.replace(/(?<!Â«[^Â«Â»]*)[ \t]*([â»Â°ï¼)}])(?![^Â«Â»]*Â»)/g,'$1')
  //   * replace all '/' with 'â‹…/'
  input = input.replace(/(?<!Â«[^Â«Â»]*)\/(?![^Â«Â»]*Â»)/g,'â‹…/')
  
  // uncomment the following for debugging
  // say(input)
}

// The start rule for a Peggy grammar is the first rule.  For us, it's a
// sequence of LCs.
LCs =  _ a:LC|..,__| _  { return a.join(' ') }

// A single LC
LC "LC" = @( Meta / Contradiction / Given / Declaration / Environment / Expression)

// givens
Given "Given" = (':' / 'Assume'i __ ) 
  a:(Environment / Expression) { return ':\\text{:}'+a }

// environments
Environment "Environment" = 
  '{' _ a:( Meta / Declaration / Given / Environment / Expression )|..,__| _ '}'
  { return `\\{ ${a.join(' ')} \\}` }

// things it searches for and replaces up front
Meta = Putdown / Comment / Shorthand / StringLiteral

// unprocessed putdown notation (cannot include // comments)
Putdown = 'Â«' @$([^Â»]*) 'Â»'  // todo: add a non-unicode way to do this

// insert a comment that gets echoed
Comment "Comment" = ('Comment'i / 'âž¤') __ a:StringLiteral { return `(âž¤ ${a})` }
// String Literals for comments
StringLiteral = $('"' [^"]* '"')

// Shorthand symbols that will be post-processed
Shorthand = BIH / Ruleset / Rule / Thm / Proof

BIHLabel = 'since'i / 'because'i / 'Recall'i
BIH = BIHLabel { return '\\text{Recall }' }
RulesetLabel = 'Rules'i / 'Axioms'i / 'Givens'i / 'Definitions'i
Ruleset = RulesetLabel ':'? { return '\\text{Rules:}' }
RuleLabel = 'Rule'i / 'Axiom'i / 'Given'i / 'Definition'i
Rule = RuleLabel ':'? { return '\\text{Rule}' }
ThmLabel = 'Theorem'i / 'Thm'i / 'Lemma'i / 'Corollary'i
Thm = ThmLabel ':'? { return '\\text{Theorem}' }
ProofLabel = 'Proof'i 
Proof = ProofLabel ':'? { return '\\text{Proof:}' }

// declarations
Declaration "Declaration" = ForSome / Let

ForSome = body:Expression __ 'for'i __ 'some'i __ a:SymbolSeq 
          { return `${body}\\text{ for some }${a}` }
// the 'given' colon is optional since these are always 'given'.          
Let =  'Let'i __ a:SymbolSeq __ ('be'i __)? 'such'i __ 'that'i __ b:Expression 
       { return `\\text{Let }${a}\\text{ be such that }${b}` } /
       'Let'i __ a:SymbolSeq { return `\\text{Let }${a}` }

// expressions - get's Stuff from Not
Expression "Expression" = Quantified / Binding / Prop / EFA  

// quantified binding expressions
Quantified = a:BindingSymbol b:Binding
          { return `\\left(${a} ${b}\\right)` }

// binding expressions
Binding = a:Symbol (period / 'â†¦') b:(Expression / Binding) 
          { return `${a}, ${b}` }

// propositional expressions
Prop = Iff / Implies / And / Or / Not

Contradiction = a:'contradiction'           { return '\\Leftarrow\\Rightarrow' }

Iff           = a:Implies|1..,'â‡”'/' iff '|    { return texJoin('\\Leftrightarrow',a) }
Implies       = a:Or|1..,'â‡’'/' implies '|     { return texJoin('\\Rightarrow',a) }
Or            = a:And|1..,' or '/'âˆ¨'|         { return texJoin('\\text{ or }',a) }
And           = a:Not|1..,' and '/'âˆ§'|        { return texJoin('\\text{ and }',a) }
Not           = a:('Â¬'/'not ')? _ b:Stuff     { return (a==='not ' || a==='Â¬')
                                                        ?texUnary('\\neg',b)
                                                        :b
                                              }

// stuff that might be a prop
Stuff = Relations / Thing

// relations
Relations = Maps / Partition / Congruent / Subset / ElementOf / NotEltOf / Divides / 
            Leq / LessThan / Relation / Equation / NotEqual / Loves / Is

Maps       = a:Thing _ ':' _ b:Thing 'â†’' c:Thing      { return `a\\colon ${b}\\to ${c}` }

Partition  = a:(Binding/Thing) ' is a partition of 'i b:(Binding/Thing)
             { return texJoin('\\text{ is a partition of }',[a,b]) }
Congruent  = a:(Binding/Thing) 'â‰ˆ' b:(Binding/Thing) ' mod 'i c:(Binding/Thing)
             { return `${a}\\underset{${c}}{\\equiv}${b}` }
Subset     = a:Thing|2..,'âŠ†'/' subset '/' subseteq '| { return texJoin('\\subseteq',a) }
NotEltOf   = a:Thing (' notin ') b:Thing              { return texJoin('\\in',[a,b])        } 
ElementOf  = a:Thing ('âˆˆ'/' in ') b:Thing             { return texJoin('\\in',[a,b]) }
Divides    = a:Thing ('|'/' divides ') b:Thing        { return texJoin('\\mid',[a,b]) }
Leq        = a:Thing|2..,'â‰¤'/' leq '|                 { return texJoin('\\leq',a) }
LessThan   = a:Thing|2..,'<'/' lt '|                  { return texJoin('<',a) }
NotEqual   = a:Thing (' neq '/' ne ') b:Thing         { return texJoin('\\neq',[a,b]) }
Relation   = a:(Binding/Thing)|2..,'~'|               { return texJoin('\\sim',a) }        
Equation   = a:(Binding/Thing)|2..,'='|               { return texJoin('=',a) }
Loves      = a:Thing|2..2,' loves '|                  { return texJoin('\\text{ loves }',a) }
Is         = a:Thing|2..2,' is an '/' is a '/' is '|  { return texJoin('\\text{ is }',a) }


// things to relate
Thing = Set / Algebraic

// sets
Set = Composition / CartProd / Union / Intersection / RelativeComp / Complement

Composition   = a:Atomic|2..,'âˆ˜'/' circ '|            { return texJoin('\\circ',a) }
CartProd      = a:CompAtomic|2..,'Ã—'/' times '|  { return texJoin('Ã—',a) }
Union         = a:CompAtomic|2..,'âˆª'/' cup '/' union '|  { return texJoin('\\cup',a) }
Intersection  = a:CompAtomic|2..,'âˆ©'/' cap '/' intersect '|  { return texJoin('\\cap',a) }
RelativeComp  = a:CompAtomic|2..,'âˆ–'/' setminus '|  { return texJoin('\\setminus',a) }
// we don't want to have to do e.g. Aâˆ©(BÂ°) instead of just Aâˆ©BÂ°
CompAtomic    = Complement / Atomic
Complement    = a:Atomic ('Â°'/' complement') { return `${a}^\\circ` }

// algebraic expressions
Algebraic = Sum / Product

Sum       = a:Product b:([-+] Product)+             { return texSum(a,b)   }
Product   = a:(Recip/Factorial/Inversed/Negated)|1..,'â‹…'/' cdot '| 
                                                    { return texJoin('',a) }
Recip     = a:Atomic ('â»'/' recip')                 { return `${a}^-`      }
Factorial = a:Atomic ('ï¼'/' factorial')            { return `${a}!`       }
Inversed  = a:'/'? b:Exp                            { return texUnary(a,b) }
Negated   = a:'-'? b:Exp                            { return texUnary(a,b) }
Exp       = a:Atomic|1..,'^'|                       { return texRightAssoc('^',a) } 

// morally atomic (do not require parentheses)
Atomic =   EFA / Prefix / Parenthesized / EquivalenceClass / Tuple / Symbol / Number

// TODO: fix this hack for inverse functions
Prefix = a:(Symbol ('â»'/' recip') / Symbol / Parenthesized ) 
         b:( '(' @Expression|..,comma| ')' )+ 
  { if (Array.isArray(a)) return `${a[0]}^-\\left(${b}\\right)` 
    return `${a}\\left(${b}\\right)` 
  }
  

// For convenience we define ðœ†P(k) to be (Î» P k) and replace Î» with "LDE EFA"
// as a shortcut
EFA = 'Î»' a:Symbol '(' b:Expression ')' { return `\\lambda{${a}}(${b})` }

EquivalenceClass = '[' b:(  @Expression|1..2,comma| )+ ']' { 
  // if the optional relation is missing from an equivalence class, don't print it
  if (b.length===1 && b[0].length===1) {
    return `\\left[${b[0]}\\right]` 
  } else {
    return `\\left[${b[0]}\\right]_{${b[1]}}` 
  }
}
Tuple = 'âŸ¨' b:(  @Expression|1..,comma| )+ 'âŸ©' { return `\\langle{${b}}\\rangle` }
Parenthesized = '(' a:Expression ')' { return `\\left(${a}\\right)` }
 

// while we probably will never use a number or reserved symbol as a bound var, 
// we might Declare one to be a constant, e.g. Peano or ops
// TODO: try to unify the lists of reserved symbols in one place
DeclareSeq = a:(Symbol/Number/ReservedChars)|1..,comma| { return a.join(' ') }
// numbers
Number  = Decimal / Natural
Decimal = $( Natural '.' [0-9]+ )
Natural = $( [1-9][0-9]* / '0' )

// Symbols can't start with a digit, contain reserved constants, or keywords
SymbolSeq = a:(Symbol/Number)|1..,comma| { return a.join(',') }
Symbol "Symbol" = $( 'â‰¡' / 'â†’â†' / '~' / '"' [^"]* '"' /
                     !([0-9]/ReservedWord)
                     [^-\][()âŸ¨âŸ© \t\nâœ¦{},.:+â‹…/^=<â‰¤Â¬â†’â†â‡’â‡”|âˆ€âˆƒâˆ©âˆªÃ—âˆˆâŠ†âˆ–â»Â°âˆ˜âˆ§âˆ¨â‰¡â†¦~â‰ˆï¼]+ )
BindingSymbol = ('âˆƒ!'/'exists unique ') { return '\\exists!' } /
                ('âˆ€'/'forall '/'for all ') { return '\\forall' } /
                ('âˆƒ'/'exists ') { return '\\exists' } 

// punctuation
comma  =  ','
period =  '.'
__  = [ \t\n\r]+
_   = [ \t\n\r]*

ReservedChars = $([:+â‹…/^=<â‰¤Â¬â†’â†â‡’â‡”|âˆ€âˆƒâˆ©âˆªÃ—âˆˆâŠ†âˆ–â»Â°âˆ˜âˆ§âˆ¨â‰¡ÏƒÎ»â†¦~â‰ˆï¼])
ReservedWord = ' or '/' and '/' loves '/' cdot '/' complement' / 
                ' setminus '/' cap '/' cup '/' times '/' lt '/' leq '/
                ' in '/' subset '/' implies '/' iff ' / 'maps ' /
                ' contradiction ' / ' divides '